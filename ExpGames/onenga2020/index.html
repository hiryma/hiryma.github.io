<!DOCTYPE html>
<html>

<head>
	<head prefix="og: http://ogp.me/ns# fb: http://ogp.me/ns/fb# article: http://ogp.me/ns/article#">
	<meta property="og:type" content="article"/>
	<meta property="og:title" content="【KAI運TAP】"/>
	<meta property="og:description" content="新年あけましておめでとうございます。 いつもお世話になっている皆さまに感謝の気持ちを込めて、カヤックからお年賀をお届けいたします。"/>
	<meta property="og:site_name" content="面白法人カヤック"/>
	<meta property="og:url" content="https://hiryma.github.io/ExpGames/onenga2020/index.html"/>
	<meta property="og:image" content="https://hiryma.github.io/ExpGames/onenga2020/ogp.png"/>

	<link rel="icon" type="image/png" href="favicon.png">
	<style type="text/css">
		<!--
		.center {
			text-align: center;
		}

		.floatLeft {
			text-align: left;
			float: left;
		}

		.hidden {
			display: none;
		}

		/*
		canvas {
			border-style: solid;
			border-width: 2px;
		}
		*/
		-->
	</style>
	<meta charset="utf-8">
	<title>カヤック年賀2020 </title>
</head>

<body>
	<div class="center">
		<canvas id="screen"></canvas>
	</div>
	<div class="hidden">
		<img id="uiAtlas" src="uiAtlas.png">
		<img id="bg" src="bg.png">
	</div>

	<script id="3dvs" type="x-shader/x-vertex">
precision mediump float;

attribute vec2 aCoord; // theta, y

varying vec3 vNormal;
varying vec3 vPosition;
varying vec4 vAlbedo;

uniform vec4 uTransform0;
uniform vec4 uTransform1;
uniform vec4 uTransform2;
uniform vec4 uTransform3;
uniform vec4 uToWorld0;
uniform vec4 uToWorld1;
uniform vec4 uToWorld2;
uniform vec3 uToWorldNormal0;
uniform vec3 uToWorldNormal1;
uniform vec3 uToWorldNormal2;
uniform vec4 uPolynominal; //0,1,2,3次の順
uniform float uPolynominal4; //4次の項
uniform vec4 uAlbedo;
uniform vec2 uRainbow; // x:0か1 y:time

void main(){
	float dirX = cos(aCoord.x);
	float dirZ = sin(aCoord.x);
	float y = aCoord.y;
	float l;
	l = uPolynominal4;
	l *= y;
	l += uPolynominal.w;
	l *= y;
	l += uPolynominal.z;
	l *= y;
	l += uPolynominal.y;
	l *= y;
	l += uPolynominal.x;

	vec4 p = vec4(dirX * l, y, dirZ * l, 1.0);

	gl_Position.x = dot(uTransform0, p);
	gl_Position.y = dot(uTransform1, p);
	gl_Position.z = dot(uTransform2, p);
	gl_Position.w = dot(uTransform3, p);

	// 法線生成
	float dl;
	dl = 4.0 * uPolynominal4;
	dl *= y;
	dl += 3.0 * uPolynominal.w;
	dl *= y;
	dl += 2.0 * uPolynominal.z;
	dl *= y;
	dl += uPolynominal.y;
	// タンジェントは2次元座標で(1,dl)なので、これを3次元に移す
	vec3 n;
	n.x = dirX;
	n.z = dirZ;
	n.y = -dl;
	n = normalize(n);

	vNormal.x = dot(uToWorldNormal0, n);
	vNormal.y = dot(uToWorldNormal1, n);
	vNormal.z = dot(uToWorldNormal2, n);

	vPosition.x = dot(uToWorld0, p);
	vPosition.y = dot(uToWorld1, p);
	vPosition.z = dot(uToWorld2, p);

	// 以下虹効果
	float rad = aCoord.x + (y * 1.0) + uRainbow.y;
	float c = cos(rad);
	float s = sin(rad);

	float e = 0.577350269189626;
	float e2 = e * e;
	float invC = 1.0 - c;
	float e2InvC = e2 * invC;
	float es = e * s;
	vec3 m = vec3(c + e2InvC, e2InvC - es, e2InvC + es);
	float yz = 0.707106781186548 * 0.75;
	vec3 v = vec3(0, yz, -yz);

	vec3 rotV;
	rotV.x = dot(m.xyz, v);
	rotV.y = dot(m.zxy, v);
	rotV.z = dot(m.yzx, v);
	rotV += vec3(0.8);
	vec3 rainbowAlbedo = clamp(rotV, 0.4, 1.0);
	vAlbedo.xyz = mix(uAlbedo.xyz, rainbowAlbedo, uRainbow.x);
	vAlbedo.w = uAlbedo.w;
}
</script>

	<script id="3dfs" type="x-shader/x-fragment">
precision mediump float;
uniform sampler2D uSampler;
uniform vec3 uEmission;
uniform vec3 uLightVector;
uniform float uFresnel0;

varying vec3 vNormal; // world
varying vec3 vPosition; // world
varying vec4 vAlbedo;

vec2 fresnel2(float f0, vec2 eh_en){
	vec2 t = vec2(1.0) - eh_en;
	vec2 t2 = t * t;
	return vec2(f0) + (t2 * t2 * t) * (1.0 - f0);
}

void main(){
	// constants
	vec3 lightColor = vec3(0.4, 0.4, 0.4);
	vec3 eyePosition = vec3(0.0, 0.0, -10.0);
	vec3 ambientDown = vec3(0.6, 0.4, 0.5);
	vec3 ambientUp = vec3(0.5, 0.5, 0.5);
	float f0 = uFresnel0;
	float shininess = 256.0;

//	float pi = 3.14159;
	float directSpecularCoeff = 41.061975317708997; // (shininess + 2.0) / (2.0 * pi)

	vec3 l = uLightVector;
	vec3 e = normalize(eyePosition - vPosition);
	vec3 n = normalize(vNormal);
	vec3 h = normalize(l + e);
	vec4 dots = vec4(dot(n, l), dot(n, h), dot(e, h), dot(e, n));
	dots = clamp(dots, vec4(0.0), vec4(1.0));
	vec2 fresnels = fresnel2(f0, dots.zw);
	float ry = -e.y + (n.y * (2.0 * dots.w));

	vec3 directDiffuse = lightColor * dots.x;
	vec3 indirectDiffuse = mix(ambientDown, ambientUp, (n.y * 0.5) + 0.5); // 半球ライト
	vec3 diffuse = (directDiffuse + indirectDiffuse) * vAlbedo.xyz * (1.0 - f0);

	vec3 directSpecular = lightColor * (fresnels.x * pow(dots.y, shininess) * directSpecularCoeff);
	vec3 indirectSpecular = mix(ambientDown, ambientUp, (ry * 0.5) + 0.5) * fresnels.y;
	vec3 specular = directSpecular + indirectSpecular;

	gl_FragColor.xyz = diffuse + specular + uEmission;
	gl_FragColor.w = mix(vAlbedo.w, 1.0, fresnels.y);
}
</script>

	<script id="2dvs" type="x-shader/x-vertex">
precision mediump float;
attribute vec2 aPosition;
attribute vec2 aTexCoord;
attribute vec4 aColor;
varying vec2 vTexCoord;
varying vec4 vColor;
uniform vec3 uTransform0;
uniform vec3 uTransform1;

void main(){
	vec3 p = vec3(aPosition.xy, 1.0);
	gl_Position.x = dot(uTransform0, p);
	gl_Position.y = dot(uTransform1, p);
	gl_Position.z = -1.0;
	gl_Position.w = 1.0;
	vTexCoord = aTexCoord;
	vColor = aColor;
}
</script>

	<script id="2dfs" type="x-shader/x-fragment">
precision mediump float;
uniform sampler2D uSampler;
varying vec2 vTexCoord;
varying vec4 vColor;

void main(){
	gl_FragColor = texture2D(uSampler, vTexCoord) * vColor;
}
</script>

	<script type="text/javascript" src="libKayac.js"></script>
	<script type="text/javascript" src="main.js"></script>
</body>

</html>